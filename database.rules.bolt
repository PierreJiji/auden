path / {
  write() { isAdmin() }
  read() { isAdmin() }
}

// User can check if they are admin or not...
path /admins/{uid} {
  read() { isCurrentUser(uid) }
}

// User can write to their own profile
path /profiles/{uid} is Profile {
  read() { true }
  write() { isCurrentUser(uid) }
}
type Profile {
  displayName: String
}

// Anyone can see which scene is active
path /currentScene is String {
  read() { true }
}

path /scenes {
  path /quiz {
    // Everyone can read the current state of quiz
    path /state {
      read() { true }
    }
    // Audience can read information about current question only.
    path /questions/{qid} {
      read() { this.parent().parent().currentQuestion == qid }
    }
    // Audience can submit answer to current question.
    path /answers/{qid}/{uid} is Answer {
      read() { isCurrentUser(uid) }
      write() { isCurrentUser(uid) && this.parent().parent().parent().currentQuestion == qid }
    }
  }
}
type Answer {
  answerId: String
  timestamp: CurrentTimestamp
}
type CurrentTimestamp extends Number {
  validate() { this == now }
}

isAdmin() {
  root.admins[auth.uid] == true
}
isCurrentUser(uid) {
  auth != null && auth.uid == uid
}
